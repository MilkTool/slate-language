
"Various utilities, should probably be moved elsewhere."

s@(Sequence traits) splitPreservingEmptys: obj &count: count
"Divides the Sequence up into subsequences as delimited by the given element."
[| subSeqs keyStart keyEnd |
  subSeqs: ExtensibleArray new.
  keyEnd: (s indexOf: obj startingAt: 0 ifAbsent: [s size]).
  subSeqs add: (s copyFrom: 0 to: keyEnd - 1).
  [keyEnd < s size] whileTrue:
    [count isNotNil /\ [subSeqs size >= count] ifTrue:
       [subSeqs add: (s copyFrom: keyEnd to: s size - 1).
        ^ subSeqs].
     keyStart: ((s indexOf: obj startingAt: keyEnd ifAbsent: [keyEnd]) + 1).
     keyEnd: (s indexOf: obj startingAt: keyStart ifAbsent: [s size]).
     keyStart <= keyEnd ifTrue:
       [subSeqs add: (s copyFrom: keyStart to: keyEnd - 1)]].
  subSeqs
].

"Int16 streams"

prototypes define: #Int16ReadStream &parents: {StreamProcessor}.
Int16ReadStream define: #LittleEndian &parents: {Int16ReadStream}.
Int16ReadStream define: #BigEndian &parents: {Int16ReadStream}.

prototypes define: #Int16WriteStream &parents: {WriteStream}.
Int16WriteStream define: #LittleEndian &parents: {Int16WriteStream}.
Int16WriteStream define: #BigEndian &parents: {Int16WriteStream}.

Int16ReadStream LittleEndian traitsWindow 
		atSlotNamed: #printName
		put: 'Int16ReadStream LittleEndian'.
Int16ReadStream BigEndian traitsWindow 
		atSlotNamed: #printName 
		put:'Int16ReadStream BigEndian'.
Int16WriteStream LittleEndian traitsWindow 
		 atSlotNamed: #printName 
		 put: 'Int16WriteStream LittleEndian'.
Int16WriteStream BigEndian traitsWindow 
		 atSlotNamed: #printName 
		 put: 'Int16WriteStream BigEndian'.

Int16WriteStream LittleEndian addSlot: #target.
Int16WriteStream BigEndian addSlot: #target.

"FIXME: when reading from streams check that they aren't shorter than
16 bits"

s@(Int16WriteStream LittleEndian traits) on: target
[
  s target: target.
  s
].

s@(Int16WriteStream BigEndian traits) on: target
[
  s target: target.
  s
].

s@(Int16ReadStream LittleEndian traits) next
[| byte1 byte2 |
  byte1: (s source next as: Integer).
  byte2: (s source next as: Integer).
  (byte2 bitShift: 8) bitOr: byte1
].

s@(Int16ReadStream BigEndian traits) next
[| byte1 byte2 |
  byte1: (s source next as: Integer).
  byte2: (s source next as: Integer).
  (byte1 bitShift: 8) bitOr: byte2
].

s@(Int16WriteStream LittleEndian traits) nextPut: aCharacter
[
  s target nextPut: ((aCharacter bitAnd: 16r00FF) as: ASCIIString Character).
  s target nextPut: ((aCharacter bitShift: -8) as: ASCIIString Character).
].

s@(Int16WriteStream BigEndian traits) nextPut: aCharacter
[
  s target nextPut: ((aCharacter bitShift: -8) as: ASCIIString Character).
  s target nextPut: ((aCharacter bitAnd: 16r00FF) as: ASCIIString Character).
].

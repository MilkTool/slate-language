streams define: #LimitedProcessor &parents: {DelegatingProcessor} &slots: {
  #count -> 0
}.

s@(LimitedProcessor traits) for: n
[
  s clone `>> [count := n. ]
].

s@(LimitedProcessor traits) isAtEnd
[
  s count <= s count zero \/ [resend]
].

s@(LimitedProcessor traits) next: n putInto: seq startingAt: start
[
  s count >= n
    ifTrue: [s count -= n. resend]
    ifFalse: [n := s count. s count := 0. resend]
].

streams define: #InOutProcessor &parents: {StreamProcessor} &slots: {
  #inputBuffer.
  #outputBuffer
}.

p@(InOutProcessor traits) on: s
[
  resend `>> [inputBuffer := s source collectionType new. outputBuffer := s collectionType new. ]
].

s@(InOutProcessor traits) canProcessMore
[
  s inputBuffer isNotEmpty \/ [s source isAtEnd not]
].

s@(InOutProcessor traits) next: n putInto: seq startingAt: start
[| buf amt |
  [s outputBuffer size < n /\ [s canProcessMore]]
    whileTrue: [s bufferInputTo: s requiredInSize. s process].
  amt := n min: s outputBuffer size.
  seq replaceFrom: start to: start + amt - 1 with: s outputBuffer.
  s outputBuffer := s outputBuffer allButFirst: amt.
  amt
].

s@(InOutProcessor traits) isAtEnd
[
  s outputBuffer isEmpty /\ [s inputBuffer isEmpty] /\ [resend]
].

s@(InOutProcessor traits) requiredInSize
"This is the amount we try to get into the in buffer every time. Useful
if you're looking for a search string of a specified size. Override in child classes."
[
  4
].

"doesn't/can't guarantee that it's filled!"
s@(InOutProcessor traits) bufferInputTo: neededSize
[| nextBuffer amt needed |
  s inputBuffer size >= neededSize ifFalse:
    [needed := neededSize - s inputBuffer size.
     nextBuffer := s source collectionType new &capacity: needed.
     amt := s source next: needed putInto: nextBuffer.
     s inputBuffer := s inputBuffer ; (nextBuffer first: amt)].
].

s@(InOutProcessor traits) process
[
  s passInThrough
].

s@(InOutProcessor traits) passInThrough
[
  s write: s inputBuffer.
  s inputBuffer := s source collectionType new.
].

s@(InOutProcessor traits) write: seq
[
  s outputBuffer := s outputBuffer ; seq
].

s@(InOutProcessor traits) skip: amt
[
  s inputBuffer := s inputBuffer allButFirst: amt
].


s@(InOutProcessor traits) guaranteedReadable
[
  s outputBuffer size
].

streams define: #SimpleReplacementStream &parents: {InOutProcessor} &slots: {
  #search.
  #replace
}.

s@(SimpleReplacementStream traits) find: x replaceWith: y
[
  s new `>> [search: x. replace: y. ]
].

s@(SimpleReplacementStream traits) requiredInSize
[
  s search size max: 1
].

"fixme don't cons as much"
s@(SimpleReplacementStream traits) process
[
  s search size < 1 ifTrue: [^ s passInThrough].

  [s inputBuffer size >= s requiredInSize]
    whileTrue: 
      [(s inputBuffer first: s search size) = s search 
         ifTrue: [s write: s replace. s skip: s search size]
         ifFalse: [s write: (s inputBuffer first: 1). s skip: 1 ]].

  s source isAtEnd ifTrue: [s passInThrough]
].

s@(Sequence traits) find: find@(Sequence traits) replaceWith: replace@(Sequence traits)
[
  [| :out |
   s reader >> (SimpleReplacementStream find: find replaceWith: replace) >> out
  ] writingAs: s
].

streams define: #MultiProcessor &parents: {DelegatingProcessor} &slots: {#children}.

mp@(MultiProcessor traits) using: s
[
  mp new `>> [children: s. ]
].

mp@(MultiProcessor traits) on: s
[
  mp children isEmpty ifTrue: [^ (error: 'No children for processor')].

  "fixme we should be able to write the following in a better way"
  mp children first on: s.
  (1 to: mp children size - 1 by: 1) do: [|:i| (mp children at: i) on: (mp children at: i - 1)].
  mp source: mp children last.

  mp
].

d@(Dictionary traits) keysAndValuesCollect: block
[
  d isEmpty ifTrue: [^ #{}].
  result ::= ExtensibleArray new.
  d keysAndValuesDo: [| :key :val | result add: (block applyWith: key with: val)].
  result
].

s@(Sequence traits) sequentialFindAndReplace: dict@(Dictionary traits)
[
  [| :out |
   s reader
     >> (MultiProcessor using: 
           (dict keysAndValuesCollect: [| :key :val | SimpleReplacementStream find: key replaceWith: val]))
     >> out] writingAs: s
].

streams define: #MultiReplacementStream &parents: {InOutProcessor} &slots: {
  #dict -> Dictionary new.
  #cachedSize
}.

s@(MultiReplacementStream traits) from: d@(Dictionary traits)
[
  s new `>> [dict := d. ]
].

s@(MultiReplacementStream traits) requiredInSize
[
  s cachedSize `defaultsTo: (s dict keys collect: #size `er) max
].

s@(MultiReplacementStream traits) process
[| hasMatched |
  s requiredInSize < 1 ifTrue: [^ s passInThrough].
  [s inputBuffer size >= 1]
    whileTrue: 
      [hasMatched := False.
       s dict keysAndValuesDo:
         [| :key :val |
          (s inputBuffer beginsWith: key)
            ifTrue:
              [s write: val.
               s skip: key size.
               hasMatched := True]].
       hasMatched ifFalse:
         [s write: s inputBuffer first.
          s skip: 1]].
].

s@(Sequence traits) findAndReplace: dict@(Dictionary traits)
[
  [| :out |
   s reader >> (MultiReplacementStream from: dict) >> out
  ] writingAs: s
].

"todo: look at using extensible arrays"
streams define: #LineProcessor &parents: {InOutProcessor} &slots: {#currentLine}.

s@(LineProcessor traits) collectionType [Array].

s@(LineProcessor traits) process
[| pos lastChar |
  s currentLine `defaultsTo: (s inputBuffer newSize: 0).
  (pos := s inputBuffer indexOfFirstSatisfying: [| :char | lastChar := char. char = $\n \/ [char = $\r]])
    ifNil:
      [s currentLine := s currentLine ; s inputBuffer.
       s inputBuffer := s inputBuffer new.
       ^ Nil].
  (s currentLine := s currentLine ; (inputBuffer first: pos))
    isEmpty ifFalse: [s currentLine last = $\r ifTrue: [s currentLine := s currentLine allButLast]].
  s write: s currentLine.
  s inputBuffer := inputBuffer allButFirst: pos.
  s bufferInputTo: 1.
  s inputBuffer isEmpty not /\ [s inputBuffer first = $\n] /\ [lastChar = $\r]
    ifTrue: [s inputBuffer := s inputBuffer allButFirst].
].

s@(LineProcessor traits) next: n putInto: seq startingAt: start
[| buf amt |
  [s outputBuffer size < n /\ [s canProcessMore]] whileTrue:
    [s bufferInputTo: s requiredInSize. s process].
  amt := n min: s outputBuffer size.
  seq replaceFrom: start to: start + amt - 1 with: s outputBuffer.
  s outputBuffer := s outputBuffer allButFirst: amt.
  amt
].


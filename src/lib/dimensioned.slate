numerics ensureNamespace: #units &delegate: True.
"Based on the Squeak Units code at http://home.netsurf.de/helge.horch/squeak/units.html"

numerics define: #Dimension &parents: {Cloneable}.

d1@(Dimension traits) isConsistentWith: d2@(Dimension traits) [d1 isSameAs: d2].

numerics define: #Length &parents: {Dimension}.

numerics define: #Time &parents: {Dimension}.

numerics define: #Mass &parents: {Dimension}.

numerics define: #Temperature &parents: {Dimension}.

numerics define: #Count &parents: {Dimension}.

numerics define: #ElectricalCharge &parents: {Dimension}.

numerics define: #Unit.

u@(Unit traits) units
[{u}].

_@(Unit traits) exponents
[#{1}].

u1@(Unit traits) isConsistentWith: u2@(Unit traits) [u1 dimension isConsistentWith: u2 dimension].

u1@(Unit traits) * u2@(Unit traits)
[
  exponentsByUnit ::= OrderedDictionary newFrom: u1 units to: u1 exponents.
  u2 unitsAndExponentsDo: [| :unit :expon | (exponentsByUnit includesKey: unit)
    ifTrue: [exponentsByUnit at: unit += expon]
    ifFalse: [exponentsByUnit at: unit := expon]].
  (ComplexUnit units: exponentsByUnit keys exponents: exponentsByUnit values) reduced
].

u1@(Unit traits) / u2@(Unit traits)
[u1 * u2 reciprocal].

u1@(Unit traits) per: u2@(Unit traits)
[u1 / u2].

u@(Unit traits) raisedTo: expon
[
  ComplexUnit units: {u} exponents: {expon}
].

u@(Unit traits) reciprocal
[u raisedTo: -1].

u@(Unit traits) squared
[u raisedTo: 2].

u1@(Unit traits) factor: u2@(Unit traits)
[
  u2 * (u1 bases / u2 bases)
].

u@(Unit traits) unitsAndExponentsDo: block
"Evaluate the block once for each unit/exponent pair in u, defaulting to this."
[block applyWith: u with: 1].

u@(Unit traits) conversionFactorTo: v@(Unit traits)
"Assuming u and v are consistent, return the numerical answer to u*X=v."
[u conversionFactor / v conversionFactor].

u@(Unit traits) additiveFactor
"The value of the base level of the scale - usually 0; the only thing that
has a real standard non-zero value is Temperatures (by old convention)."
[0].

_@(Unit traits) consistentWith: _@(Unit traits)
[False].

numerics define: #ObjectUnit &parents: {Unit} &slots: {
  #object -> Nil. "The object/exemplar on which the Unit is based."
  #comparisonBlock -> #= `er. "How to tell if units are compatible; the caller needs to consider this."
}.
"A Unit used to describe/count Slate objects."

u@(ObjectUnit traits) isConsistentWith: v@(ObjectUnit traits)
[u comparisonBlock apply*, u object, v object].

u@(ObjectUnit traits) isConsistentWith: v@(Unit traits)
[False].

u@(Unit traits) isConsistentWith: v@(ObjectUnit traits)
[False].

u@(ObjectUnit traits) printOn: s@(PrettyPrinterMixin traits)
[s ; 'of: ' ; (RootedPath from: here to: u object) printString].

u@(ObjectUnit traits) printFullOn: s@(PrettyPrinterMixin traits) plural: p
[
  p ifTrue: [s ; u object printName plural]
    ifFalse: [s ; u object printName]
].

u@(ObjectUnit traits) dimension
[u object].

numerics define: #Measure &parents: {Number} &slots: {#unit. #value}.

m@(Measure traits) unit: unit value: value
[m clone `>> [unit := unit. value := value. ]].

m@(Measure traits) dimension [m unit dimension].

m@(Measure traits) printOn: s@(PrettyPrinterMixin traits) &full
[
  m value printOn: s.
  s ; ' '.
  (full ifNil: [True])
    ifTrue: [m unit printFullOn: s plural: m value ~= 1]
    ifFalse: [m unit printOn: s].
  m
].

x@(Number traits) as: m@(Measure traits)
[
  m unit: ScalarUnit value: x
].

m@(Measure traits) as: x@(Number traits)
[m value].

m@(Measure traits) convertTo: u@(Unit traits)
"Convert the value to have the same units as u, using scaling factors."
[
  (m unit isConsistentWith: u) ifTrue:
    [m unit: u value: (m value - m unit additiveFactor) * (m unit conversionFactorTo: u) + u additiveFactor]
].

m@(Measure traits) in: u@(Unit traits)
"Alias of convertTo:."
[m convertTo: u].

m@(Measure traits) bases
"Return the reduction of m to base units."
[
  m convertTo: m unit baseUnits
].

m@(Measure traits) as: u@(Unit traits)
[
  m convertTo: u
].

x@(Number traits) of: u@(Unit traits)
[
  Measure unit: u value: x
].

x@(Number traits) of: obj &comparison
[| u |
  u := ObjectUnit `>> [object := obj. comparisonBlock ?= comparison. ].
  Measure unit: u value: x
].

x@(Number traits) as: u@(Unit traits)
[
  x of: u
].

x@(Measure traits) isConsistentWith: y@(Measure traits)
[
  x unit isConsistentWith: y unit
].

x@(Measure traits) + y@(Measure traits)
[
  (x isConsistentWith: y)
   ifTrue:
     [(x unit: x unit value: x value + y value) reduced]
   ifFalse: "TODO: raise condition" []
].

x@(Measure traits) + y
[
  x value + y as: x unit
].

x + y@(Measure traits)
[
  y + x
].

x@(Measure traits) - y@(Measure traits)
[
  x + y negated
].

x@(Measure traits) - y
[
  x value - y as: x unit
].

x - y@(Measure traits)
[
  x - y value as: x unit
].

x@(Measure traits) * y@(Measure traits)
[
  (x unit: x unit * y unit value: x value * y value) reduced
].

x@(Measure traits) * y
[
  x value * y as: x unit
].

x * y@(Measure traits)
[
  y * x
].

x@(Measure traits) / y@(Measure traits)
[
  (y unit: x unit / y unit value: x value / y value) reduced
].

x@(Measure traits) / y
[
  x value / y as: x unit
].

x / y@(Measure traits)
[
  x / y value as: x unit
].

x@(Measure traits) = y@(Measure traits)
[
  (x isConsistentWith: y) /\ [y value = (x as: y) value]
].

x@(Measure traits) < y@(Measure traits)
[
  (x isConsistentWith: y) /\ [x value < (y as: x unit) value]
].

m@(Measure traits) negated
[m value negated as: m unit].

m@(Measure traits) reduced
"Answer the scalar part if the units have 'vanished'."
[
  (m unit isConsistentWith: ScalarUnit) ifTrue: [m value] ifFalse: [m]
].

uv1@(Measure traits) factor: uv2@(Measure traits)
"Factor with respect to another unit or unitValue."
[
  uv1 factor: uv2 unit
].

m@(Measure traits) factor: u@(Unit traits)
[
  m as: (m unit factor: u).
].

m@(Measure traits) raisedTo: expon
[
  m unit: (m unit raisedTo: expon) value: (m value raisedTo: expon)
].

m@(Measure traits) reciprocal
[m raisedTo: -1].

m@(Measure traits) sqrt
[
  m unit: (m unit raisedTo: 1 / 2) value: (m value sqrt)
].

m@(Measure traits) ceiling
[
  m value ceiling as: m unit
].

m@(Measure traits) floor
[
  m value floor as: m unit
].

m@(Measure traits) roundTo: x
[
  (m value roundTo: x) as: m unit
].

m@(Measure traits) roundUpTo: x
[
  (m value roundUpTo: x) as: m unit
].

m@(Measure traits) truncateTo: x
[
  (m value truncateTo: x) as: m unit
].

m@(Measure traits) rounded
[
  (m value rounded) as: m unit
].

numerics define: #CompoundUnit &parents: {Unit} &slots: {#units -> #{}. #exponents -> #{}}.
"Represents units which are a direct combination of BaseUnit objects, vs. DerivedUnit/PrefixedUnit/etc. objects."

CompoundUnit traits compareAndHashUsingSlots: #{#units. #exponents}.

cu@(CompoundUnit traits) copy
[resend `>> [units := cu units copy. exponents := cu exponents copy. ]].

u@(Unit traits) as: cu@(CompoundUnit traits)
[
  cu units: {u} exponents: {1}
].

cu@(CompoundUnit traits) as: _@(CompoundUnit traits)
[
  cu
].

cu@(CompoundUnit traits) units: units exponents: expons
[
  cu clone `>> [units := units. exponents := expons. ]
].

cu@(CompoundUnit traits) reciprocal
[
  cu units: cu units
     exponents: (cu exponents collect: #negated `er)
].

cu@(CompoundUnit traits) raisedTo: expon
[
  cu units: cu units
     exponents: (cu exponents collect: #(* expon) `er)
].

cu@(CompoundUnit traits) unitsAndExponentsDo: block
[
  cu units with: cu exponents do: block
].

cu@(CompoundUnit traits) conversionFactor
[1].

cu@(CompoundUnit traits) reduced
[
  `conditions: {
    [cu units isEmpty] -> [ScalarUnit].
    [cu units size = 1 /\ [cu expons first = 1]] -> [cu units first]
  } otherwise: [cu]
].

cu@(CompoundUnit traits) printOn: s@(PrettyPrinterMixin traits)
[| first any count |
  first := True.
  any := False.
  cu unitsAndExponentsDo: [| :unit :expon |
    expon isPositive
      ifTrue: [any := True.
        first ifFalse: [s nextPut: $*].
        first := False.
        unit printOn: s.
        expon ~= 1 ifTrue: [s nextPut: $^. expon printOn: s]]].
  count := cu exponents inject: 0 into: [| :c :each |
    each isNegative ifTrue: [c + 1] ifFalse: [c]].
  count isPositive
    ifTrue: [any ifFalse: [s nextPut: $1].
      s nextPut: $/.
      count > 1 ifTrue: [s nextPut: $(].
      first := True.
      cu unitsAndExponentsDo: [| :unit :expon |
        expon ~= 1 ifTrue: [first ifFalse: [s nextPut: $*].
        first := False.
        unit printOn: s.
        expon < -1 ifTrue: [s nextPut: $^. expon negated printOn: s]]].
      count > 1 ifTrue: [s nextPut: $)]].
  cu
].

cu@(CompoundUnit traits) printFullOn: s@(PrettyPrinterMixin traits) plural: b
[| pos neg |
  pos := cu exponents anySatisfy: #isPositive `er.
  neg := cu exponents anySatisfy: #isNegative `er.
  pos ifTrue: [cu printUnitsWhereExponent: #isPositive `er
                  on: s plural: b].
  neg /\ pos ifTrue: [s nextPut: $\s].
  neg ifTrue: [s ; 'per '.
    cu printUnitsWhereExponent: #isNegative `er on: s plural: b].
  cu
].

cu@(CompoundUnit traits) printUnitsWhereExponent: block on: s plural: plural
[| power first count index thisPlural |
  first := True.
  count := cu exponents count: block.
  index := 0.
  cu unitsAndExponentsDo: [| :unit :expon |
    (block applyWith: expon)
      ifTrue: [first ifTrue: [first := False] ifFalse: [s nextPut: $\s].
        index += 1.
        thisPlural := plural /\ [index = count].
        power := expon abs.
        (power is: Integer) /\ [power <= 3]
          ifTrue: [power = 2 ifTrue: [s ; 'square '].
            power = 3 ifTrue: [s ; 'cubic ']].
        unit printFullOn: s plural: plural.
        power > 3 \/ [(power is: Integer) not] ifTrue:
          [s nextPut: $^. power printOn: s]]].
  cu
].

cu1@(CompoundUnit traits) isConsistentWith: cu2@(CompoundUnit traits)
[
  cu1 units = cu2 units /\ [cu1 bases = cu2 bases]
].

cu@(CompoundUnit traits) dimension
[
  cu unitsAndExponentsDo: [| :unit :expon |
    "TODO: assemble/return a compound dimension"
  ].
].

numerics define: #ComplexUnit &parents: {CompoundUnit} &slots: {
  #conversionFactor.
  #cachedBases
}.
"Represents units which are a combination of some non-BaseUnit objects."

cu@(ComplexUnit traits) units: units exponents: exponents
[
  result ::= resend `>> [conversionFactor := 1. cachedBases := Nil. ].
  units with: exponents do:
    [| :unit :expon |
     result conversionFactor *= unit conversionFactor raisedTo: expon].
  result
].

cu@(ComplexUnit traits) copy
[resend `>> [cachedBases := cu cachedBases copy. ]].

cu@(ComplexUnit traits) bases
"Calculating bases is expensive, so the values are cached."
[
  cu cachedBases `defaultsTo: cu calculateBases
].

cu@(ComplexUnit traits) calculateBases
[| bases exponentsByUnit newUnits newExpons scratch |
  cu unitsAndExponentsDo:
    [| :unit :expon |
     (bases := unit bases) unitsAndExponentsDo:
       [| :subunit :subexpon |
        scratch := subexpon * expon.
        (exponentsByUnit includesKey: subunit)
          ifTrue: [exponentsByUnit at: subunit += scratch]
          ifFalse: [exponentsByUnit at: subunit := scratch]]].
  newUnits := ExtensibleArray new.
  newExpons := ExtensibleArray new.
  (exponentsByUnit keySet sortedBy: [| :l :r | l abbrev < r abbrev]) do:
    [| :unit |
     (scratch := exponentsByUnit at: unit) isZero ifFalse:
       [newUnits add: unit. newExpons add: scratch]].
  CompoundUnit units: newUnits exponents: newExpons
].

cu1@(ComplexUnit traits) isConsistentWith: cu2@(ComplexUnit traits)
[
  cu1 bases isConsistentWith: cu2 bases
].

numerics define: #ScalarUnit &parents: {Unit}.

_@(ScalarUnit traits) units [#{}].
_@(ScalarUnit traits) exponents [#{}].

_@(ScalarUnit traits) isConsistentWith: other@(CompoundUnit traits)
[
  other units isEmpty /\ [other bases isEmpty]
].

_@(ScalarUnit traits) isConsistentWith: _@(Unit traits)
[False].

_@(Unit traits) isConsistentWith: _@(ScalarUnit traits)
[False].

_@(ScalarUnit traits) isConsistentWith: _@(ScalarUnit traits)
[True].

x@(ScalarUnit traits) reciprocal
[x].

x@(ScalarUnit traits) raisedTo: expon
[x].

x@(ScalarUnit traits) unitsAndExponentsDo: block
[Nil].

_@(ScalarUnit traits) conversionFactor
[1].

x@(ScalarUnit traits) printOn: s@(PrettyPrinterMixin traits)
["Do-nothing"].

numerics define: #ModifiedUnit &parents: {Unit} &slots: {#base. #modification}.

"A base unit with a modification that makes it incompatible with anything
not having the same modification, good for domain-specific use.
e.g. 1.6 moles -> 1.6 moles of sulfuric acid. (Strings not required)"

mu@(ModifiedUnit traits) base: u modification: mod
[mu clone `>> [base := u. modification := mod. ]].

ModifiedUnit traits compareAndHashUsingSlots: #{#base. #modification}.

mu@(ModifiedUnit traits) bases
[
  mu
].

mu@(ModifiedUnit traits) conversionFactor
[
  mu base conversionFactor
].

mu@(ModifiedUnit traits) modify: mod
[
  mu base: mu modification: mod
].

mu@(ModifiedUnit traits) printOn: s@(PrettyPrinterMixin traits)
[| mod |
  mu base printOn: s.
  s ; '('.
  ((mod := mu modification) is: String)
    ifTrue: [s ; mod]
    ifFalse: [mod printOn: s].
  s ; ')'.
  mu
].

mu@(ModifiedUnit traits) printFullOn: s@(PrettyPrinterMixin traits) plural: plural
[| mod |
  mu base printFullOn: s plural: plural.
  s ; '( of '.
  ((mod := mu modification) is: String)
    ifTrue: [s ; mod]
    ifFalse: [mod printOn: s].
  s ; ')'.
  mu
].

mu@(ModifiedUnit traits) isConsistentWith: cu@(ComplexUnit traits)
[
  mu isConsistentWith: cu bases
].

mu1@(ModifiedUnit traits) isConsistentWith: mu2@(ModifiedUnit traits)
[
  mu1 base = mu2 base /\ [mu1 modification = mu2 modification]
].

numerics define: #PrefixedUnit &parents: {Unit} &slots: {#base. #prefix}.

"A Unit with an SI Prefix attached."

pu@(PrefixedUnit traits) prefix: prefix base: unit
[pu clone `>> [prefix := prefix. base := unit. ]].

pu@(PrefixedUnit traits) prefixedBy: prefix
"Combine the prefixes."
[pu clone `>> [prefix *= prefix. ]].

pu@(PrefixedUnit traits) dimension [pu base dimension].

pu@(PrefixedUnit traits) isConsistentWith: u@(Unit traits)
[
  pu base isConsistentWith: u
].

u@(Unit traits) isConsistentWith: pu@(PrefixedUnit traits)
[
  pu base isConsistentWith: u
].

pu@(PrefixedUnit traits) conversionFactor
[
  pu prefix scalingFactor * pu base conversionFactor
].

pu@(PrefixedUnit traits) printOn: s@(PrettyPrinterMixin traits)
[
  s ; pu prefix abbrev.
  pu base printOn: s.
  pu
].

pu@(PrefixedUnit traits) printFullOn: s@(PrettyPrinterMixin traits) plural: plural
[
  s ; pu prefix prefixName.
  pu base printFullOn: s plural: plural.
  pu
].

numerics define: #SIPrefix &parents: {Cloneable}
  &slots: {#abbrev. #prefixName. #scalingFactor}.
"SI Prefixes:
 http://physics.nist.gov/cuu/Units/prefixes.html
 http://www.bipm.org/en/si/prefixes.html
Main SI site:
 http://www.bipm.org/en/si/
A Standard interchange format for SI units and numbers
 http://swiss.csail.mit.edu/~jaffer/MIXF/"

units ensureNamespace: #Prefixes.
units Prefixes ensureNamespace: #ByName &delegate: True.
units Prefixes ensureNamespace: #ByAbbrev &delegate: True.

sip@(SIPrefix traits) name: name abbrev: abbrev scalingFactor: n
[| result |
  result := sip clone `>> [abbrev := abbrev. prefixName := name. scalingFactor := n. ].
  units Prefixes ByName addImmutableSlot: name intern valued: result.
  units Prefixes ByAbbrev addImmutableSlot: abbrev intern valued: result.
  result
].

sip@(SIPrefix traits) named: name
[
  units SIPrefixesByName at: name intern
].

sip@(SIPrefix traits) abbrev: name
[
  units SIPrefixesByAbbrev at: name intern
].

u@(Unit traits) prefixedBy: prefix@(SIPrefix traits)
[
  PrefixedUnit prefix: prefix base: u
].

p1@(SIPrefix traits) * p2@(SIPrefix traits)
[| scalingFactor |
  scalingFactor := p1 scalingFactor * p2 scalingFactor.
  units SIPrefixesByName keysAndValuesDo: [| :key :value |
    value scalingFactor = scalingFactor ifTrue: [^ value]].
  Nil
].

SIPrefix name: 'yotta' abbrev: 'Y' scalingFactor: (10 raisedTo: 24).
SIPrefix name: 'zetta' abbrev: 'Z' scalingFactor: (10 raisedTo: 21).
SIPrefix name: 'exa' abbrev: 'E' scalingFactor: (10 raisedTo: 18).
SIPrefix name: 'peta' abbrev: 'P' scalingFactor: (10 raisedTo: 15).
SIPrefix name: 'tera' abbrev: 'T' scalingFactor: (10 raisedTo: 12).
SIPrefix name: 'giga' abbrev: 'G' scalingFactor: (10 raisedTo: 9).
SIPrefix name: 'mega' abbrev: 'M' scalingFactor: (10 raisedTo: 6).
SIPrefix name: 'kilo' abbrev: 'k' scalingFactor: (10 raisedTo: 3).
SIPrefix name: 'hecto' abbrev: 'h' scalingFactor: (10 raisedTo: 2).
SIPrefix name: 'deca' abbrev: 'da' scalingFactor: (10 raisedTo: 1).
SIPrefix name: 'deci' abbrev: 'd' scalingFactor: (10 raisedTo: -1).
SIPrefix name: 'centi' abbrev: 'c' scalingFactor: (10 raisedTo: -2).
SIPrefix name: 'milli' abbrev: 'm' scalingFactor: (10 raisedTo: -3).
SIPrefix name: 'micro' abbrev: 'u' scalingFactor: (10 raisedTo: -6).
SIPrefix name: 'nano' abbrev: 'n' scalingFactor: (10 raisedTo: -9).
SIPrefix name: 'pico' abbrev: 'p' scalingFactor: (10 raisedTo: -12).
SIPrefix name: 'atto' abbrev: 'a' scalingFactor: (10 raisedTo: -15).
SIPrefix name: 'femto' abbrev: 'f' scalingFactor: (10 raisedTo: -18).
SIPrefix name: 'zepto' abbrev: 'z' scalingFactor: (10 raisedTo: -21).
SIPrefix name: 'yocto' abbrev: 'y' scalingFactor: (10 raisedTo: -24).

numerics define: #NamedUnit &parents: {Unit}
  &slots: {#abbrev. #unitName. #pluralName}.

nu@(NamedUnit traits) name: unitName abbrev: abbrev &plural: pluralName
[
  pluralName `defaultsTo: unitName plural.
  nu clone `>> [abbrev := abbrev. unitName := unitName. pluralName := pluralName. ]
].

nu@(NamedUnit traits) printOn: s@(PrettyPrinterMixin traits)
[
  s ; nu abbrev.
  nu
].

nu@(NamedUnit traits) printFullOn: s@(PrettyPrinterMixin traits) plural: plural
[
  plural ifTrue: [s ; nu unitName] ifFalse: [s ; nu pluralName].
  nu
].

numerics define: #BaseUnit &parents: {NamedUnit}.

"Defines core SI units and allows for basic extension."

bu@(BaseUnit traits) modify: mod
[
  ModifiedUnit base: bu modification: mod
].

bu@(BaseUnit traits) bases
[bu].

bu@(BaseUnit traits) conversionFactor
[1].

bu1@(BaseUnit traits) isConsistentWith: bu2@(BaseUnit traits)
[bu1 == bu2].

x@(Measure traits) / y@(Unit traits)
[x / (1 of: y)].

bu@(BaseUnit traits) isConsistentWith: cu@(ComplexUnit traits)
[bu isConsistentWith: cu bases].

cu@(CompoundUnit traits) isConsistentWith: cu2@(BaseUnit traits)
"Compound units always have a non-trivial set of units."
[False].

mu@(ModifiedUnit traits) isConsistentWith: bu@(BaseUnit traits)
[False].

bu@(BaseUnit traits) isConsistentWith: mu@(ModifiedUnit traits)
[False].

_@(ModifiedUnit traits) is: _@(BaseUnit traits)
"Modified units are effectively new bases."
[True].

units ensureNamespace: #SI &delegate: True.
units SI ensureNamespace: #ByName &delegate: True.
units SI ensureNamespace: #ByAbbrev &delegate: True.
units SI ensureNamespace: #ByPluralName &delegate: True.

bu@(BaseUnit traits) name: unitName abbrev: abbrev dimension: dimension &plural: pluralName
[| newBU |
  pluralName `defaultsTo: unitName plural.
  newBU := bu name: unitName abbrev: abbrev &plural: pluralName.
  [| :n | n of: newBU] asMethod: unitName intern on: {Number traits}.
  [| :n | n of: newBU] asMethod: abbrev intern on: {Number traits}.
  newBU traits addImmutableSlot: #dimension valued: dimension.
  units SI ByName addImmutableSlot: unitName intern valued: newBU.
  units SI ByAbbrev addImmutableSlot: abbrev intern valued: newBU.
  units SI ByPluralName addImmutableSlot: pluralName intern valued: newBU.
  newBU
].

BaseUnit name: 'gram' abbrev: 'g' dimension: Mass.
BaseUnit name: 'meter' abbrev: 'm' dimension: Length.
BaseUnit name: 'second' abbrev: 's' dimension: Time.
units SI ByAbbrev addImmutableSlot: #sec valued: units SI ByAbbrev s.
BaseUnit name: 'candela' abbrev: 'c' dimension: Count &plural: 'candela'.
BaseUnit name: 'mole' abbrev: 'mol' dimension: Count.
BaseUnit name: 'kelvin' abbrev: 'K' dimension: Temperature &plural: 'kelvin'.

bu@(BaseUnit traits) named: name
[
  name intern sendTo: {units SI ByName}
].

bu@(BaseUnit traits) abbrev: name
[
  name intern sendTo: {units SI ByAbbrev}
].

bu@(BaseUnit traits) pluralNamed: name
[
  name intern sendTo: {units SI ByPluralName}
].

cu@(ComplexUnit traits) isConsistentWith: bu@(BaseUnit traits)
[bu isConsistentWith: cu bases].

numerics define: #DerivedUnit &parents: {NamedUnit} &slots: {#unitValue}.

d@(DerivedUnit traits) dimension [d unitValue dimension].

"This defines a unit in terms of powers of other units, plus a scalar."

units ensureNamespace: #ByName &delegate: True.
units ensureNamespace: #ByAbbrev &delegate: True.
units ensureNamespace: #ByPluralName &delegate: True.

du@(DerivedUnit traits) name: unitName abbrev: abbrev value: val &plural: pluralName
[| newU |
  pluralName `defaultsTo: unitName plural.
  newU := du name: unitName abbrev: abbrev &plural: pluralName.
  newU unitValue := val.
  [| :n | n of: newU] asMethod: unitName intern on: {Number traits}.
  [| :n | n of: newU] asMethod: abbrev intern on: {Number traits}.
  units ByName addImmutableSlot: unitName intern valued: newU.
  units ByAbbrev addImmutableSlot: abbrev intern valued: newU.
  units ByPluralName addImmutableSlot: pluralName intern valued: newU.
  newU
].

du@(DerivedUnit traits) unit
[
  du unitValue unit
].

du@(DerivedUnit traits) dimension
[
  du unitValue dimension
].

du@(DerivedUnit traits) bases
[
  du unitValue unit bases
].

du@(DerivedUnit traits) conversionFactor
[
  du unitValue value * du unitValue unit conversionFactor
].

du@(DerivedUnit traits) isConsistentWith: bu@(BaseUnit traits)
[
   du unitValue = 1 /\ [bu isConsistentWith: du unit]
].

bu@(BaseUnit traits) isConsistentWith: du@(DerivedUnit traits)
[
  du unitValue = 1 /\ [bu isConsistentWith: du unit]
].

DerivedUnit
  name: 'centimeter'
  abbrev: 'cm'
  value: (1 of: (units m prefixedBy: units Prefixes c)).

DerivedUnit
  name: 'kilometer'
  abbrev: 'km'
  value: (1 of: (units m prefixedBy: units Prefixes k)).

DerivedUnit
  name: 'kilogram'
  abbrev: 'kg'
  value: (1 of: (units g prefixedBy: units Prefixes k)).

DerivedUnit
  name: 'inch'
  abbrev: 'in'
  value: (2.54 of: units cm).

DerivedUnit
  name: 'foot'
  abbrev: 'ft'
  value: (12 of: units in)
  &plural: 'feet'.

DerivedUnit
  name: 'yard'
  abbrev: 'yd'
  value: (3 of: units ft).

DerivedUnit
  name: 'mile'
  abbrev: 'mi'
  value: (5280 of: units ft).

DerivedUnit
  name: 'acre'
  abbrev: 'acre'
  value: (4046.87260987 of: units m squared).

DerivedUnit
  name: 'newton'
  abbrev: 'N'
  value: (1 of: units kg * units m / units s squared)
  &plural: 'newtons'.

DerivedUnit
  name: 'minute'
  abbrev: 'min'
  value: (60 of: units s).

DerivedUnit
  name: 'hour'
  abbrev: 'h'
  value: (60 of: units min).

DerivedUnit
  name: 'day'
  abbrev: 'd'
  value: (24 of: units h).

DerivedUnit
  name: 'year'
  abbrev: 'yr'
  value: (365.242198781 of: units d).

DerivedUnit
  name: 'hertz'
  abbrev: 'Hz'
  value: (1 of: units s) reciprocal
  &plural: 'hertz'.

numerics define: #TemperatureUnit &parents: {DerivedUnit}
  &slots: {#additiveFactor}.

tu@(TemperatureUnit traits) name: unitName abbrev: abbrev value: val additiveFactor: af &plural: pluralName
[
  (tu name: unitName abbrev: abbrev value: val &plural: pluralName) `>> [additiveFactor := af. ]
].

TemperatureUnit
  name: 'degree Rankine'
  abbrev: 'R'
  value: (5 / 9 of: units K)
  additiveFactor: 0
  &plural: 'degrees Rankine'.

TemperatureUnit
  name: 'degree Fahrenheit'
  abbrev: 'F'
  value: (5 / 9 of: units K)
  additiveFactor: -459.67
  &plural: 'degrees Fahrenheit'.

TemperatureUnit
  name: 'degree Celsius'
  abbrev: 'C'
  value: (1 of: units K)
  additiveFactor: -273.15
  &plural: 'degrees Celsius'.

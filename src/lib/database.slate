
"for talking to databases through a socket interface"


"
TODO:

 - postgres needs better error handling
 - accept any type of message at any time
 - support for advanced features

"

lobby ensureNamespace: #DB &delegate: False.


DB define: #Database &parents: {Cloneable} &slots: {#verbose -> False}.
DB define: #DatabaseCommand &parents: {DB Database} &slots: {}.
DB define: #DatabaseReply &parents: {DB Database} &slots: {}.


db@(DB Postgres traits) inform: str
[
  db verbose ifTrue: [lobby inform: str].
].


DB define: #Postgres &parents: {DB Database} &slots: {#stream -> Nil}.
DB define: #PostgresReply &parents: {DB DatabaseReply} &slots: {#type -> Nil. #length -> 0. #response -> Nil}.
DB define: #PostgresCommand &parents: {DB DatabaseCommand. StructMixin}.
DB define: #PostgresStartCommand &parents: {DB PostgresCommand}
  &slots: {#length -> 0. #protocol -> 196608. #user -> 'user'. #database -> 'mydb'.
           #binaryDescription -> #((length Int32)
                                   (protocol Int32)
                                   ('user\0' Fixed)
                                   (user CString)
                                   ('database\0' Fixed)
                                   (database CString)
                                   ('\0' Fixed))}.

DB define: #PostgresPasswordMessage &parents: {DB PostgresCommand}
  &slots: {#length -> 0. #password -> ''.
           #binaryDescription -> #(('p' Fixed)(length Int32)(password CString))}.

DB define: #PostgresQueryCommand &parents: {DB PostgresCommand}
  &slots: {#length -> 0. #query -> ''.
           #binaryDescription -> #(('Q' Fixed)(length Int32)(query CString))}.


DB define: #PostgresColumnDescription &parents: {Cloneable}
 &slots: {#name -> ''. #objectID -> 0. #attributeNumber -> 0. #type -> 0. #typeSize -> 0. #typeModifier -> 0. #format -> 0}.

db@(DB Database traits) newOn: stream
[
overrideThis
].

db@(DB Database traits) sendCommand: cmd@(DB DatabaseCommand traits)
[
overrideThis
].

db@(DB Database traits) close
[
overrideThis
].

reply@(DB DatabaseReply traits) isError
[
overrideThis
].


" ... Postgresql specific ... "

pc@(DB PostgresCommand traits) littleEndian
[
  False
].

db@(DB Postgres traits) newOn: stream
[ | ret |
  ret: db new.
  db stream: stream.
  db
].

db@(DB Postgres traits) sendCommand: cmd@(DB PostgresCommand traits)
[
  db stream ; cmd packed.
  db stream flush.
].

db@(DB Postgres traits) close
[
  db inform: 'closing postgres connection'.
  db stream close.
].

db@(DB Postgres traits) hasReply
[
  db stream socket canRead
].

reply@(DB PostgresReply traits) newType: type response: response
[ | return |
  reply new `>> [type: type. length: response size. response: response. ]
].


db@(DB Postgres traits) connectTo: addr@(Net SocketAddress traits)
[ | socket |
  socket: (Net Socket newFor: addr domain type: Net Socket Types Stream protocol: Net Socket Protocols Default).
  socket connectTo: addr.
  db stream: (Net SocketStream newOn: socket).
  db
].

db@(DB Postgres traits) connectTo: addr@(String traits)
[ 
  db connectTo: (addr as: Net SocketAddress)
].


reply@(DB PostgresReply traits) isNotice
[
  reply type = $N code
].


reply@(DB PostgresReply traits) isError
[
  reply type = $E code
].

reply@(DB PostgresReply traits) printError
[
  reply isError \/ [reply isNotice] ifFalse: [^ Nil].
  reply response first = 0
        ifTrue: ['Generic Error']
        ifFalse: ['Postgres Error: ' ; (reply response allButFirst as: ASCIIString)]
].


db@(DB Postgres traits) readReply
[ | responseTypeByte responseLength response |

[
  responseTypeByte: db stream next.
  responseLength: ((db stream next: 4) as: Integer &bigEndian: True)."fixme replace with int32 constant size or something"
  response: ((responseLength >= 4)
               ifTrue: [(db stream next: responseLength - 4)] 
               ifFalse: [error: 'malformed message from ' ; db printString]).
 "fixme coerce to real structure"
  DB PostgresReply newType: responseTypeByte response: response

] on: Stream Exhaustion do: [ |:e| ^ Nil]

].

db@(DB Postgres traits) md5Encrypt: pw salt: salt
[
  ((pw as: ByteArray) ; (salt as: ByteArray)) md5String
].

db@(DB Postgres traits) handleLogin: user password: pw
[ | cmd reply reply authCode|
  reply: db readReply.
  db inform: 'reply: ' ; reply printString.
  reply isError ifTrue: [error: 'Login Failed. ' ; reply printError].
  reply type ~= $R code ifTrue: [error: 'Reply from server is not an authentication request'].
  authCode: ((reply response first: 4) as: Integer &bigEndian: True).
  db inform: 'auth code: ' ; authCode printString.
  authCode = 0 ifTrue: [ ^ True].
  authCode = 5 "md5 with salt on pw"
    ifTrue: [db inform: 'salt: ' ; (reply response allButFirst: 4) printString.
             cmd: (DB PostgresPasswordMessage new `>> [password: 'md5' ; (db md5Encrypt: (db md5Encrypt: pw salt: user) salt: (reply response allButFirst: 4)). ]).
             cmd length: cmd packed size - 1.
             db inform: 'sending md5 pw: ' ; cmd password printString.
             db sendCommand: cmd.
             ^ (db handleLogin: user password: pw)
             ]
    ifFalse: [error: 'unhandled authentication'].

].

db@(DB Postgres traits) loginAs: user password: pw &database: database
"Use this command to log into a database"
[ | cmd |
  database `defaultsTo: user.
  cmd: (DB PostgresStartCommand new `>> [user: user. database: database. ]).
  cmd length: cmd packed size.
  db sendCommand: cmd.
  (db handleLogin: user password: pw) ifTrue: [db inform: 'Login success'].
  db waitForQuery.
].


db@(DB Postgres traits) waitForQuery
"receive messages until readyforquery message"
[ | reply |
 [
  reply: db readReply.
  reply type caseOf: {
    $S code -> [db inform: 'parameter status ' ; reply printString].
    $E code -> [error: 'Error: ' ; reply printError. ].
    $K code -> [db inform: 'Receiving Key data'].
    $Z code -> [db inform: 'Ready for query'. ^ True].
  } otherwise: [error: 'unexpected reply: ' ; reply printString]
 ] loop.
].



db@(DB Postgres traits) query: query
"Use this to do a simple query on the database"
[ | cmd |
  cmd: (DB PostgresQueryCommand new `>> [query: query. ]).
  cmd length: cmd packed size - 1.
  db sendCommand: cmd.
  db readQueryResult
].

pcd@(DB PostgresColumnDescription traits) createEntryFrom: value
[
  pcd format = 0 ifTrue: [value as: ASCIIString]
                 ifFalse: [value "fixme.. binary serialize"]
].

db@(DB Postgres traits) readQueryResult
[ | reply result rowDescription rows reader columnCount|
 result: ExtensibleArray new.
 rows: ExtensibleArray new.
 [
  reply: db readReply.
  reader: reply response reader.
  reply type caseOf: {
    $N code -> [db inform: 'Notice: ' ; reply printError].
    $C code -> [db inform: 'Command Complete'. result add: {rowDescription. rows}].
    $E code -> [error: 'Read Query Result Error: ' ; reply printError. ].
    $I code -> [db inform: 'Empty query response.'].
    $D code -> [db inform: 'Data row'.
                columnCount: ((reader next: 2) as: Integer &bigEndian: True).
                rows add: ((0 below: columnCount)
                                collect: [ | :col entrySize entry|
                                            entrySize: ((reader next: 4) as: Integer &bigEndian: True).
                                            entrySize < 0 ifTrue: [Nil]
                                                          ifFalse: [(rowDescription at: col) createEntryFrom: (reader next: entrySize)]]).
                ].
    $T code -> [db inform: 'Row description.'. 
                rowDescription: (Array newSize: ((reader next: 2) as: Integer &bigEndian: True)).
                0 below: rowDescription size do:
                  [ | :i column |
                     column: DB PostgresColumnDescription new.
                     column name: ((reader nextUntil: [ |:c| c = 0]) as: ASCIIString).
                     reader next. "null terminator"
                     column objectID: ((reader next: 4) as: Integer &bigEndian: True).
                     column attributeNumber: ((reader next: 2) as: Integer &bigEndian: True).
                     column type: ((reader next: 4) as: Integer &bigEndian: True).
                     column typeSize: ((reader next: 2) as: Integer &bigEndian: True).
                     column typeModifier: ((reader next: 4) as: Integer &bigEndian: True).
                     column format: ((reader next: 2) as: Integer &bigEndian: True).
                     db inform: 'column: ' ; column printString.
                     rowDescription at: i put: column.
                  ]].
    $Z code -> [db inform: 'Ready for query'. ^ result].
  } otherwise: [error: 'unexpected reply: ' ; reply printString]
 ] loop.
].



node@(nodes Node traits) bindTo: value &environment: env
[
  overrideThis
].

_@(nodes Placeholder traits) bindTo: val &environment: env
[
  val
].

literal@(nodes Literal traits) bindTo: val &environment: env
[| lhs |
  lhs: literal value.
  (lhs is: Symbol) \/ [lhs is: String]
    ifTrue:
      [(nodes UnaryMessage sending: lhs intern to: {nodes ImplicitArgument})
         bindTo: val &environment: env]
    ifFalse:
      [error: 'Cannot bind this type of literal: ' ; lhs printString]
].

load@(nodes LoadVariable traits) bindTo: value &environment: env
[
  `(load `unquote
      ifNil: [(load store: value) `unquote]
      ifNotNil: [error: 'Cannot rebind'])
].

msg@(nodes UnaryMessage traits) bindTo: value &environment: env
[
  env isNil \/ [msg arguments first ~= nodes ImplicitArgument]
    ifTrue:
      [nodes KeywordMessage sending: #bind:to: to:
         {msg arguments first. nodes Literal for: msg selector. value}]
    ifFalse:
      [(env outermostScopeNotBinding: msg selector)
         ifNil: [error: 'Cannot rebind']
         ifNotNilDo:
           [| :scope |
            StoreVariable of: value into:
              (scope addVariableNamed: msg selector &varType: nodes Binding)]]
].

message@(nodes KeywordMessage traits) bindTo: val &environment: env
"Expands a message by looking at its selector and picking the right setter."
[
  (message selector isKeywordSelector ifTrue:
    [message selector caseOf: {
       #addSlotNamed: -> [#bind:to:].
       #addImmutableSlotNamed: -> [#bind:to:].
     } otherwise: []])
    ifNil: [error: 'Cannot rebind'. message]
    ifNotNilDo:
      [| :newSel | message sending: newSel to: (message arguments copyWith: val)]
].

"This is the tricky moment at which we activate the special macro.
Above here, it is unavailable, and after here, the definitions is not sound."
n@(nodes Node traits) ::= val &environment: env [n bindTo: val &environment: env].

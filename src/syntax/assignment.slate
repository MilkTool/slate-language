
n@(nodes Node traits) setTo: val &environment: env
"A macro to expand the node and value into an appropriate assignment for the
location that the message appears to use."
"TODO: Use type inference on arguments to detect if there should be a concrete
slot attribute or other type of accessor to verify or distinguish."
[overrideThis].

load@(nodes LoadVariable traits) setTo: val &environment: env
"Expands a local variable reference into a store to it."
[load store: val].

message@(nodes UnaryMessage traits) setTo: val &environment: env
"Makes a substitution of the unary message with a keyword message that is the
mutator form of that selector (appends a colon)."
[| var |
  env isNil \/ [(message arguments first = nodes ImplicitArgument) not]
    ifTrue: [nodes KeywordMessage
               sending: (message argument mutatorNameFor: message selector)
               to: {message argument. val}]
    ifFalse:
      [var: (env addVariableNamed: message selector).
       var store: val]
].

literal@(nodes Literal traits) setTo: val &environment: env
[| lhs |
  lhs: literal value.
  (lhs is: Symbol) \/ [lhs is: String]
    ifTrue:
      [literal value: lhs intern.
       `(addSlot: literal `unquote valued: val `unquote)]
    ifFalse:
      [(lhs is: Integer)
         ifTrue: [`(at: literal `unquote put: val `unquote)]
         ifFalse:
           [error: 'Assignment not implemented to this type of literal: ' ; lhs printString]]
].

_@(nodes Placeholder traits) setTo: val &environment: env
[
  val
].

message@(nodes KeywordMessage traits) setTo: val &environment: env
"Expands a message by looking at its selector and picking the right setter."
[
  (message selector isKeywordSelector ifTrue:
    [message selector caseOf:
      {#atSlotNamed: -> [#atSlotNamed:put:].
       #at: -> [#at:put:].
       #byteAt: -> [#byteAt:put:].
       #wordAt: -> [#wordAt:put:].
     } otherwise:
       [| selectorWithPut |
        (selectorWithPut: message selector name ; 'put:')
          isInterned ifTrue: [selectorWithPut intern]]])
    ifNil: [error: 'The selector cannot be handled via setTo:.'. message]
    ifNotNilDo:
      [| :newSel | message sending: newSel to: (message arguments copyWith: val)]
].

paren@(nodes Parenthesis traits) setTo: val &environment: env
"Performs a multiple-assignment by transforming each statement."
[| result |
  result: paren copy.
  result statements infect: [| :statement | statement setTo: val &environment: env].
  result
].

array@(nodes Array traits) setTo: vals &environment: env
"Performs a multiple-assignment."
[
  array statements size caseOf: {
    0 -> [vals].
    1 -> [array statements last setTo: `(vals `unquote first) &environment: env]
  } otherwise:
    [| result tmpVar |
     result: array copy.
     env
       ifNil:
         [TODO: 'Can\'t handle this outside of a block context.']
       ifNotNil:
         [tmpVar: ((vals is: nodes LoadVariable)
                     ifTrue: [vals variable] ifFalse: [env addVariable]).
          "result statements: array statements collectWithIndex:"
          array statements doWithIndex:
            [| :statement :index |
             result statements at: index put:
               (statement setTo:
                  (nodes KeywordMessage sending: #at: to:
                     {tmpVar load. nodes Literal for: index}) &environment: env)].
          {tmpVar store: vals. result} as: nodes Parenthesis]]
].

sub@(nodes Signature traits) setTo: literal@(nodes Literal traits) &environment: env
[
  literal value
    ifNil: [nodes KeywordMessage sending: #removeFrom: to: {sub selector. sub inputVariables as: nodes Array}]
    ifNotNil: [resend]
].

sig@(nodes Signature traits) setTo: body@(nodes Block traits) &environment: env
[| result |
  result: (sig as: nodes MethodDefinition).
  result localVariables: result localVariables ; body localVariables.
  result optionalVariables: result optionalVariables ; body optionalVariables.
  result fixVariableScopes.
  result transformBy:
    [| :node |
     (node isSameAs: nodes UnaryMessage) /\ [node arguments first = nodes ImplicitArgument]
       ifTrue: [(result localVariables detect: [| :var | var name = node selector])
                  ifNil: [node]
                  ifNotNilDo: [| :var | var load]]
       ifFalse: [node]].
  result
].

load@(nodes LoadVariable traits) defaultsTo: default &environment: env
"A macro that expands to an expression that sets a variable in the scope to
the expression in the default."
[`(load `unquote ifNil: [(load store: default parenthesize) `unquote])].

node@(nodes Node traits) defaultsTo: default &environment: env
"Conditional assignment on the value being Nil or absent.
This generalizes on Load defaultsTo:."
[`(node `unquote ifNil: [(node setTo: default &environment: env) `unquote])].

message@(nodes KeywordMessage traits) defaultsTo: default &environment: env
"Expands an at:-style selector into at:ifAbsentPut:."
[
  (message selector caseOf:
    {#atSlotNamed: -> [#atSlotNamed:ifAbsentPut:].
     #at: -> [#at:ifAbsentPut:].
     #byteAt: -> [#byteAt:ifAbsentPut:].
     #wordAt: -> [#wordAt:ifAbsentPut:].
  } otherwise:
    [| selectorWithPut |
     (selectorWithPut: message selector name ; 'ifAbsentPut:')
       isInterned ifTrue: [selectorWithPut intern]])
    ifNil: [resend]
    ifNotNilDo:
      [| :newSel | message sending: newSel to: (message arguments copyWith: `[value `unquote])]
].

node@(nodes Node traits) setToIfNotNil: value &environment: env
"Conditional assignment on the value being Nil or absent.
This generalizes on Load defaultsTo:."
[
  `(value `unquote ifNotNil: [(node setTo: value &environment: env) `unquote])
].

"This is the tricky moment at which we activate the special macro.
Above here, it is unavailable, and after here, the definitions is not sound."
n@(nodes Node traits) := val &environment: env [n setTo: val &environment: env].

node@(nodes Node traits) ?= value &environment: env
[
  node setToIfNotNil: value &environment: env
].

node@(nodes Node traits) \= value &environment: env
[
  node defaultsTo: value &environment: env
].

node@(nodes Node traits) += value &environment: env
[
  node setTo: (nodes BinaryMessage sending: #+ to: {node. value}) &environment: env
].

node@(nodes Node traits) -= value &environment: env
[
  node setTo: (nodes BinaryMessage sending: #- to: {node. value}) &environment: env
].

node@(nodes Node traits) *= value &environment: env
[
  node setTo: (nodes BinaryMessage sending: #* to: {node. value}) &environment: env
].

node@(nodes Node traits) /= value &environment: env
[
  node setTo: `(node `unquote / value `unquote) &environment: env
].

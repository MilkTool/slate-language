"TODO: remove all references of bitsize and per-architecture settings"

define: #Type &parents: {Types Type}
  &slots: {#dispatcher -> Cloneable clone}.
"A dispatching object for statically determining which pidgin method to invoke."

t@(Type traits) clone
[
  resend `>> [dispatcher := t dispatcher clone. ]
].

t@(Type traits) dependencies
[Set new `>> [addDependency: t withVisited: Set new. remove: t. ]].

c addDependency: t@(Type traits) withVisited: visited
[
].

t@(Type traits) alignment
[| bits |
  (bits := t bitSize) >= LongInt bitSize \/ [bits = 0]
    ifTrue: [LongInt bitSize]
    ifFalse: [bits]
].

t@(Type traits) byteSize
[
  (t bitSize + Char bitSize - 1) // Char bitSize
].

t@(Type traits) wordSize
[
  (t bitSize + LongInt bitSize - 1) // LongInt bitSize
].

define: #NamedType &parents: {Type} &slots: {#cName}.

define: #Pointer &parents: {Type} &slots: {#targetType}.

p@(Pointer traits) to: targetType
"Creates a new Pointer Type for the targetType."
"TODO: Map an IdentityDictionary from types to types of pointers to those
types, and unify implicitly?"
[
  p clone `>> [targetType := targetType. ]
].

p@(Pointer traits) cName ['P' ; p targetType slateName].
p@(Pointer traits) slateName ['UndefinedSlatePointer'].


_@(Pointer traits) bitSize
[
  LongInt bitSize
].

t@(Type traits) pointer
[
  Pointer to: t
].

p@(Pointer traits) = q@(Pointer traits)
[p targetType = q targetType].

c addDependency: p@(Pointer traits) withVisited: visited
[
  (visited includes: p)
    ifFalse:
      [visited include: p.
       (p targetType isSameAs: Synonym)
          ifTrue: [c addDependency: p targetType withVisited: visited]]
].

n@here define: slotName bitSize: size &parents: parents
"Automatically adds a method #cName which returns the lower-cased version of
the given name, as well as the parent Type."
[| type pointer printName |
  type := n define: slotName &parents: parents. "parents copyWith: Type)."
  printName := slotName name fromCamelCase.
  _@(type traits) cName [printName].
  _@(type traits) slateName [slotName].
"C Syntax SizeOf of: (g generateCFor: args first)"
  _@(type traits) bitSize [size]. "easy to find first digits if added or subtracted from"
  pointer := Pointer to: type.
  _@(type traits) pointer [pointer]
].

"*****fix these to get a primitive saying the wordsize*****"

define: #Integer &parents: {Type}.
define: #Signed &parents: {Integer}.
define: #Unsigned &parents: {Integer}.
define: #Char bitSize: 8 &parents: {Signed}.
define: #UnsignedChar bitSize: 8 &parents: {Unsigned}.
define: #ShortInt bitSize: 16 &parents: {Signed}.
define: #UnsignedShortInt bitSize: 16 &parents: {Unsigned}.
define: #LongInt bitSize: 32 &parents: {Signed}.
define: #UnsignedLongInt bitSize: 32 &parents: {Unsigned}.
define: #LongLongInt bitSize: 64 &parents: {Signed}.
define: #UnsignedLongLongInt bitSize: 64 &parents: {Unsigned}.
define: #Float bitSize: 32 &parents: {Type}.
define: #Double bitSize: 64 &parents: {Float}.
define: #Void bitSize: 0 &parents: {Type}.

define: #Modifier &parents: {Type}
  &slots: {#baseType -> Type}.

t@(Modifier traits) derive
[| newT lowerName |
  newT := resend.
  lowerName := newT printName copy toLowercase intern.
  _@(newT traits) modifierName [lowerName].
  [| :t | newT for: t] asMethod: lowerName on: {Type traits}.
  newT
].

t@(Modifier traits) cName [t modifierName ; ' ' ; t baseType cName].

m@(Modifier traits) for: b
[m clone `>> [baseType := b. ]].

define: #Mutability &parents: {Modifier}.
define: #Const &parents: {Mutability}.
define: #Volatile &parents: {Mutability}.

define: #Storage &parents: {Modifier}.
Storage traits define: #None &builder: [Storage clone].
Storage traits define: #Auto &builder: [Storage clone].
Storage traits define: #Static &builder: [Storage clone].
Storage traits define: #Extern &builder: [Storage clone].
Storage traits define: #Register &builder: [Storage clone].

define: #Array &parents: {Type} &slots:
  {#elementType -> UnsignedLongInt.
   #size -> Nil}.

t@(Array traits) of: elementType size: size
[t clone `>> [elementType := elementType. size := size. ]].

t@(Array traits) of: elementType
[t of: elementType size: Nil].

c addDependency: t@(Array traits) withVisited: visited
[
  (visited includes: t)
    ifFalse:
      [
        visited include: t.
        c addDependency: t elementType withVisited: visited
      ]
].

t@(Array traits) bitSize
[
  t elementType bitSize * t size
].

define: #Synonym &parents: {NamedType}
  &slots: {#targetType. #pointer -> Pointer. #isExported -> False}.

t@(Synonym traits) newNamed: name type: type
[
  t clone `>> [| :newT |
    cName := name.
    pointer := t pointer to: newT.
    targetType := type. ]
].

t@(Synonym traits) export
[
  t isExported := True.
  t
].

c addDependency: t@(Synonym traits) withVisited: visited
[
  (visited includes: t)
    ifFalse:
      [visited include: t.
       c include: t.
       c addDependency: t targetType withVisited: visited]
].

t@(Synonym traits) bitSize
[
  t targetType bitSize
].

define: #Structure &parents: {NamedType}
  &slots: {#basis. #pointer -> Pointer. #isExported -> False.
	   #elementSpecs -> ExtensibleArray new}.

t@(Structure traits) newNamed: name basedOn: basis
[
  t clone `>>
   [| :newT | cName := name. basis := basis.
    pointer := t pointer to: newT.
    "pointer rules rawAddDelegate: #basisPointer valued: basis pointer rules.
    pointer dispatcher rawAddDelegate: #basisPointer valued: basis pointer dispatcher."
    elementSpecs := basis elementSpecs copy.
    elementSpecs do:
      [| :se | [| :_ | se type] asMethod: se cName asInferenceRule
				on: {newT}]. ]
].

t@(Structure traits) newNamed: name
[
  t clone `>> [| :newT |
    cName := name.
    basis := Nil.
    pointer := t pointer to: newT.
    elementSpecs := t elementSpecs new. ]
].

t@(Structure traits) export
[
  t isExported := True.
  t
].

c addDependency: t@(Structure traits) withVisited: visited
[
  (visited includes: t)
    ifFalse:
      [
        visited include: t.
        c include: t.
        t elementSpecs do: [| :se | c addDependency: se type withVisited: visited]
      ]
].

t@(Structure traits) doElements: block
[| previousSE bits |
  previousSE := t elementSpecs first.
  block applyTo: {previousSE. 0}.
  bits := previousSE bitSize.
  t elementSpecs allButFirstDo:
    [| :se |
      previousSE packed /\ [se packed]
        ifFalse:
          [| alignment |
            alignment := se type alignment.
            previousSE packed
              ifTrue:
                [alignment := alignment max: previousSE type bitSize].
            bits := (bits / alignment) ceiling * alignment].
      block applyTo: {se. bits}.
      bits += se bitSize.
      previousSE := se]
].

t@(Type traits) walkElements: block &bitOffset: offset
[
  t walkElements: block withPath: Stack new atBitOffset: (offset ifNil: [0])
].

_@(Type traits) walkElements: _ withPath: _ atBitOffset: _
[
].

t@(Structure traits) walkElements: block withPath: path atBitOffset: baseOffset
[
  t doElements:
    [| :se :offset |
     path push: se.
     block applyTo: {path. baseOffset + offset}.
     se type walkElements: block withPath: path atBitOffset: baseOffset + offset.
     path pop]
].

t@(Structure traits) unpaddedSize
[| lastSE lastOffset |
  t elementSpecs isEmpty
    ifTrue: [^ 0].
  t doElements:
    [| :se :offset |
      lastSE := se.
      lastOffset := offset].
  lastOffset + lastSE bitSize
].

t@(Structure traits) alignment
[
  t elementSpecs inject: 0 into:
    [| :maxAlign :se | maxAlign max: se type alignment]
].

t@(Structure traits) bitSize
[| maxAlign |
  maxAlign := t alignment.
  (t unpaddedSize / maxAlign) ceiling * maxAlign
].


define: #Union &parents: {Structure}.

t@(Union traits) doElements: block
[
  t elementSpecs do:
    [| :se | block applyTo: {se. 0}]
].

t@(Union traits) unpaddedSize
[| maxSize |
  maxSize := 0.
  t doElements:
    [| :se :_ |
      maxSize := maxSize max: se type bitSize].
  maxSize
].

define: #StructureElement &parents: {NamedType}
  &slots: {#type -> UnsignedLongInt}.

se@(StructureElement traits) newNamed: name type: type
[
  se clone `>> [cName := name. type := type. ]
].

t@(Structure traits) addElement: se
[
  t elementSpecs
    do: [| :each | each cName = se cName
          ifTrue: [warn: 'Attempted Redefinition of type StructureElement, named "' ; each cName ; '".'.
            ^ each]].
  [| :_ | se type] asMethod: se cName asInferenceRule on: {t}.
  [| :_ | se type] asMethod: se cName asInferenceRule on: {t pointer}.
  t elementSpecs addLast: se
].

t@(Structure traits) addElementNamed: name &type: type
[
  type `defaultsTo: UnsignedLongInt.
  t addElement: (StructureElement newNamed: name type: type)
].

t@(StructureElement traits) bitSize
[
  t type bitSize
].

_@(StructureElement traits) packed
[
  False
].

define: #PackedStructureElement &parents: {StructureElement}
  &slots: {#bitSize -> 0}.

se@(PackedStructureElement traits) newNamed: name size: size
[
  (se newNamed: name type: UnsignedLongInt) `>> [bitSize := size. ]
].

t@(Structure traits) addPackedElementNamed: name size: size
[
  t addElement: (PackedStructureElement newNamed: name size: size)
].

_@(PackedStructureElement traits) packed
[True].

define: #Function &parents: {Type}
  &slots: {#argumentTypes -> {}. #resultType -> Void}.

fun@(Function traits) from: argumentTypes to: resultType
[
  fun clone `>>
   [| :newFun | argumentTypes := argumentTypes as: newFun argumentTypes.
    resultType := resultType. ]
].

define: #FunctionPointer &parents: {Pointer}.

fun@(Function traits) pointer
[
  FunctionPointer to: fun
].

_@(FunctionPointer traits) from: argumentTypes to: resultType
[
  (Function from: argumentTypes to: resultType) pointer
].

define: #Module &parents: {Type}.


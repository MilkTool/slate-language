Optimizer Assembly define: #Assembler &parents: {Cloneable} &slots:
{#module -> Optimizer Assembly MachineCode.
 #code.
 #labels.
 #relocations.
 #functions -> IdentityDictionary new.
 #function.
 #globalVariables -> IdentityDictionary new.
 #basicBlocks -> IdentityDictionary new}.

a@(Optimizer Assembly Assembler traits) new
[
  a clone `>>
    [| :newA |
      module: newA module new.
      code: newA module code writer.
      labels: newA module labels writer.
      relocations: newA module relocations writer.
      functions: newA functions new]
].

a@(Optimizer Assembly Assembler traits) flush
[
  a module code: a code contents.
  a module labels: a labels contents.
  a module relocations: a relocations contents.
  a module
].

a@(Optimizer Assembly Assembler traits) alignTo: byteAlignment
[
  (byteAlignment negated \\ 8)
    timesRepeat:
      [a code nextPut: 0]
].

a@(Optimizer Assembly Assembler traits) assemble: m@(Optimizer MR Module traits)
[
  m functions do: [| :f |
    a functions at: f put:
      Optimizer Assembly Label Internal clone `>>
        [name: f name.
          "type: ?."]].
  a globalVariables: a globalVariables new.
  m globalVariables do: #assemble: `er <- a.
  m functions do: #assemble: `er <- a
].

a@(Optimizer Assembly Assembler traits) serialize: _@Nil as: type
[
  type byteSize timesRepeat: [a code nextPut: 0]
].

a@(Optimizer Assembly Assembler traits) serialize: array as: type@(Types _Array traits)
[| valType |
  valType: type type.
  array allButLast: (array size - type size max: 0) do:
    [| :val |
      a serialize: val as: valType.
      a alignTo: valType byteAlignment].
  (type size - array size max: 0)
    timesRepeat:
      [a serialize: Nil as: valType.
        a alignTo: valType byteAlignment]
].

a@(Optimizer Assembly Assembler traits) label: g@(Optimizer MR GlobalVariable traits)
[
  a labels nextPut:
    (a globalVariables at: g put:
      Optimizer Assembly Label Internal clone `>>
        [name: g name.
          offset: a code position.
          type: g type. ])
].

a@(Optimizer Assembly Assembler traits) assemble: g@(Optimizer MR GlobalVariable traits)
[ 
  a alignTo: g type byteAlignment.
  a label: g.
  a serialize: g value as: g type
].

_@(Optimizer Assembly Assembler traits) functionAlignment
[
  overrideThis
].

_@(Optimizer Assembly Assembler traits) label: _
[
  overrideThis
].

a@(Optimizer Assembly Assembler traits) label: c@(Optimizer MR Constant traits)
[ 
  a labels nextPut:
    (a globalVariables at: c put:
      Optimizer Assembly Label Internal clone `>>
        [offset: a code position.
          type: c type. ])
].  

a@(Optimizer Assembly Assembler traits) assemble: c@(Optimizer MR Constant traits)
[
  a alignTo: c type byteAlignment.
  a label: c.
  a serialize: c value as: c type
].

a@(Optimizer Assembly Assembler traits) label: f@(Optimizer MR Function traits)
[
  a labels nextPut:
    (a functions at: f) `>>
      [offset: a code position. ]
].

a@(Optimizer Assembly Assembler traits) label: bb@(Optimizer MR BasicBlock traits)
[
  a labels nextPut:
    (a basicBlocks at: bb) `>>
      [offset: a code position. ]
].

a@(Optimizer Assembly Assembler traits) assemble: f@(Optimizer MR Function traits)
[
  a function: f.
  a basicBlocks: a basicBlocks new.
  f constants do: #assemble: `er <- a.
  a alignTo: a functionAlignment. 
  a label: f.
  f basicBlocks do:
    [| :bb |
      a basicBlocks at: bb put: 
        Optimizer Assembly Label Internal clone `>>
          ["type: ?." ]].
  f basicBlocks do: 
    [| :bb |
      a label: bb.
      a assemble: bb].
].

a@(Optimizer Assembly Assembler traits) assemble: bb@(Optimizer MR BasicBlock traits)
[
  bb instructions do: #assemble: `er <- a
].

_@(Optimizer Assembly Assembler traits) assemble: _@(Optimizer MR Instruction traits)
[
  overrideThis
].

a@(Optimizer Assembly Assembler traits) labelOf: bb@(Optimizer MR BasicBlock traits)
[
  a basicBlocks at: bb
].

a@(Optimizer Assembly Assembler traits) labelOf: c@(Optimizer MR Constant traits)
[
  a globalVariables at: c
].

a@(Optimizer Assembly Assembler traits) labelOf: g@(Optimizer MR GlobalVariable traits)
[
  a globalVariables at: g
].

a@(Optimizer Assembly Assembler traits) labelOf: f@(Optimizer MR Function traits)
[
  a functions at: f
].

a@(Optimizer Assembly Assembler traits) labelOf: l@(Optimizer MR Label traits)
[
  a labelOf: l target
].
        
